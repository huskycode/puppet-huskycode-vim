class vim($user, $home_dir) {
  include wget

  validate_string($user)
  validate_absolute_path($home_dir)

  case $::osfamily {
    RedHat: { $vim_package = 'vim-enhanced' }
    default: { $vim_package = 'vim' }
  }

  package { 'vim':
    ensure => installed,
    name   => $vim_package,
  }

  file { [
    "${home_dir}/.vim",
    "${home_dir}/.vim/autoload",
    "${home_dir}/.vim/bundle",
    ] :
    ensure => 'directory',
    owner  => $user,
  }

  file { "${home_dir}/.vimrc.local" :
    owner   => $user,
    replace => false,
    content => "\"Add here your custom options for vim, puppet will not override them\n",
  }

  wget::fetch { 'DownloadPathogen':
    source      => 'https://raw.github.com/tpope/vim-pathogen/master/autoload/pathogen.vim',
    destination => "${home_dir}/.vim/autoload/pathogen.vim",
    verbose     => true
  }

  file { "${home_dir}/.vim/autoload/pathogen.vim":
    owner => $user
  }

  concat { 'vimrc':
    path  => "${home_dir}/.vimrc",
    owner => $user,
    group => 'root',
    mode  => '0664',
  }

  Concat::Fragment {
    target  => 'vimrc',
  }

  concat::fragment { 'rc-header':
    target  => 'vimrc',
    content => "\" generated by Puppet module vim\n\n",
    order   => '05',
  }

  vim::rc { 'vimrc-pathogen':
    content => "execute pathogen#infect()\ncall pathogen#helptags()",
    order => '10',
  }

  vim::rc { 'vimrc-local':
    content => "if filereadable(glob(\"~/.vimrc.local\"))\n\tsource ~/.vimrc.local\nendif",
  }

  vim::rc { 'syntax on': }
  vim::rc { 'filetype plugin indent on': }
  vim::rc { 'highlight comment ctermfg=darkgray': }
  vim::rc { ':set bg=dark': }

  Package['vim']
  -> File[
    "${home_dir}/.vim",
    "${home_dir}/.vim/autoload",
    "${home_dir}/.vim/bundle"
  ]
  -> Wget::Fetch['DownloadPathogen']
  -> File["${home_dir}/.vim/autoload/pathogen.vim"]
  -> Concat['vimrc']

}
